import("//onevn/components/onevn_rewards/browser/buildflags/buildflags.gni")
import("//build/config/locales.gni")
import("//tools/grit/grit_rule.gni")
import("//tools/grit/repack.gni")

if (!is_android) {
  grit("static_resources") {
    source = "onevn_components_resources.grd"

    output_name = "onevn_components_resources_new"

    outputs = [
      "grit/onevn_components_resources.h",
      "onevn_components_static.pak",
    ]
    deps = [
      ":strings",
      "//onevn/components/onevn_extension/extension/onevn_extension",
    ]

    if (onevn_rewards_enabled) {
      deps += [
        "//onevn/components/onevn_rewards/resources",
        "//onevn/components/onevn_rewards/resources/extension:resources",
      ]
    }

    grit_flags = [
      "-E",
      "root_gen_dir=" + rebase_path(root_gen_dir, root_build_dir),
    ]
    output_dir = "$root_gen_dir/components"
    resource_ids = "//onevn/browser/resources/resource_ids"
  }

  repack("resources") {
    deps = [
      ":static_resources",
      "//onevn/components/onevn_adblock_ui:generated_resources",
      "//onevn/components/onevn_new_tab_ui:generated_resources",
      "//onevn/components/onevn_welcome_ui:generated_resources",
    ]

    sources = [
      "$root_gen_dir/components/onevn_components_static.pak",
      "$root_gen_dir/onevn/components/onevn_adblock/resources/onevn_adblock_generated.pak",
      "$root_gen_dir/onevn/components/onevn_new_tab/resources/onevn_new_tab_generated.pak",
      "$root_gen_dir/onevn/components/onevn_welcome/resources/onevn_welcome_generated.pak",
    ]

    output = "$root_gen_dir/components/onevn_components_resources.pak"
  }
}

grit("strings") {
  source = "onevn_components_strings.grd"

  outputs = [
    "grit/onevn_components_strings.h",
  ]

  foreach(locale, locales_with_fake_bidi) {
    outputs += [ "onevn_components_strings_$locale.pak" ]
  }

  output_dir = "$root_gen_dir/components"
  resource_ids = "//onevn/browser/resources/resource_ids"
}

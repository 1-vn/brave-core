import("//onevn/build/config.gni")
import("//onevn/components/onevn_ads/browser/buildflags/buildflags.gni")
import("//onevn/components/onevn_rewards/browser/buildflags/buildflags.gni")

source_set("browser") {
  public_deps = [
    "buildflags",
  ]

  sources = [
    "ads_service.h",
    "ads_service_factory.cc",
    "ads_service_factory.h",
    "ads_tab_helper.cc",
    "ads_tab_helper.h",
  ]

  deps = [
    "//base",
    "//onevn/components/onevn_ads/common",
    "//onevn/components/onevn_rewards/common",
    "//onevn/components/onevn_rewards/browser",
    "//components/dom_distiller/content/browser",
    "//components/dom_distiller/core",
    "//components/keyed_service/content",
    "//components/keyed_service/core",
    "//components/prefs",
    "//components/pref_registry",
    "//components/sessions",
    "//url",
    # for profile.h
    "//components/domain_reliability",
    "//content/public/browser",
    "//content/public/common",
  ]

  if (onevn_ads_enabled) {
    sources += [
      "ad_notification.cc",
      "ad_notification.h",
      "ads_service_impl.cc",
      "ads_service_impl.h",
      "background_helper.cc",
      "background_helper.h",
      "background_helper_linux.cc",
      "background_helper_linux.h",
      "background_helper_mac.mm",
      "background_helper_mac.h",
      "background_helper_win.cc",
      "background_helper_win.h",
      "locale_helper.cc",
      "locale_helper.h",
      "locale_helper_linux.cc",
      "locale_helper_linux.h",
      "locale_helper_mac.mm",
      "locale_helper_mac.h",
      "locale_helper_win.cc",
      "locale_helper_win.h",
      "bundle_state_database.cc",
      "bundle_state_database.h",
    ]

    deps += [
      "//onevn/vendor/bat-native-ads",
      "//onevn/components/onevn_ads/resources",
      "//onevn/components/services/bat_ads/public/cpp",
      "//components/history/core/browser",
      "//components/history/core/common",
      "//components/wifi",
      "//net",
      "//services/network/public/cpp",
      "//sql",
      "//ui/base",
      "//ui/message_center/public/cpp",
    ]

    if (is_win) {
      deps += [
        "//ui/views",
        "//ui/gfx",
      ]
    }
  }
}

source_set("testutil") {
  testonly = true

  deps = [
    ":browser",
    "//base",
    "//content/public/browser",
    "//chrome/test:test_support",
    "//testing/gtest",
  ]

  if (onevn_rewards_enabled) {
    deps += [
      "//onevn/vendor/bat-native-ledger",
    ]

    configs += [
      "//onevn/vendor/bat-native-ledger:internal_config"
    ]
  }

  if (onevn_ads_enabled) {
    deps += [
      "//onevn/vendor/bat-native-ads",
    ]

    configs += [
      "//onevn/vendor/bat-native-ads:internal_config"
    ]
  }

  sources = [
    "test_util.cc",
    "test_util.h",
  ]
}

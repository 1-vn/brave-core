import("//chrome/browser/resources/optimize_webui.gni")
import("//chrome/common/features.gni")
import("//tools/grit/grit_rule.gni")

grit("resources") {
  source = "settings_resources.grd"

  source_is_generated = optimize_webui

  outputs = [
    "grit/onevn_settings_resources.h",
    "grit/onevn_settings_resources_map.cc",
    "grit/onevn_settings_resources_map.h",
    "onevn_settings_resources.pak",
  ]

  output_dir = "$root_gen_dir/onevn/browser/resources/settings"
  resource_ids = "//onevn/browser/resources/resource_ids"
}

if (optimize_webui) {
  group("unpak") {
    deps = [
      ":unpak_onevn_settings_resources",
    ]
  }

  action("unpak_onevn_settings_resources") {
    script = "//chrome/browser/resources/unpack_pak.py"

    pak_file = "onevn_settings_resources.pak"
    out_folder = "$root_gen_dir/chrome/browser/resources/settings/settings_resources.unpak"

    inputs = [
      "$target_gen_dir/onevn_settings_resources.pak",
    ]

    outputs = [
      "${out_folder}/onevn_unpack.stamp",
    ]

    deps = [
      ":resources",
    ]

    args = [
      "--out_folder",
      rebase_path(out_folder, root_build_dir),
      "--pak_file",
      rebase_path("$target_gen_dir/${pak_file}", root_build_dir),
    ]
  }
}
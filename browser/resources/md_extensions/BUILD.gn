import("//chrome/browser/resources/optimize_webui.gni")
import("//chrome/common/features.gni")
import("//tools/grit/grit_rule.gni")

grit("resources") {
  source = "extensions_resources.grd"

  source_is_generated = optimize_webui

  outputs = [
    "grit/onevn_extensions_resources.h",
    "grit/onevn_extensions_resources_map.cc",
    "grit/onevn_extensions_resources_map.h",
    "onevn_extensions_resources.pak",
  ]

  output_dir = "$root_gen_dir/onevn/browser/resources/md_extensions"
  resource_ids = "//onevn/browser/resources/resource_ids"
}

group("unpak") {
  deps = [
    ":unpak_onevn_extensions_resources",
  ]
}

action("unpak_onevn_extensions_resources") {
  script = "//chrome/browser/resources/unpack_pak.py"

  pak_file = "onevn_extensions_resources.pak"
  out_folder = "$root_gen_dir/chrome/browser/resources/md_extensions/extensions_resources.unpak"

  inputs = [
    "$target_gen_dir/onevn_extensions_resources.pak",
  ]

  outputs = [
    "${out_folder}/onevn_unpack.stamp",
  ]

  deps = [
    ":resources",
  ]

  args = [
    "--out_folder",
    rebase_path(out_folder, root_build_dir),
    "--pak_file",
    rebase_path("$target_gen_dir/${pak_file}", root_build_dir),
  ]
}

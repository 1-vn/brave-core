import("//onevn/build/config.gni")
import("//onevn/components/onevn_ads/browser/buildflags/buildflags.gni")
import("//onevn/components/onevn_rewards/browser/buildflags/buildflags.gni")
import("//testing/test.gni")
import("//third_party/widevine/cdm/widevine.gni")

group("fix_onevn_test_install_name") {
  testonly = true
  deps = [
    "//onevn/vendor/challenge_bypass_ristretto_ffi:test_install_name"
  ]
}

static_library("onevn_test_support_unit") {
  testonly = true
  sources = [
    "base/onevn_unit_test_suite.cc",
    "base/onevn_unit_test_suite.h",
    "base/run_all_unittests.cc",
  ]

  public_deps = [
    "//chrome/test:test_support",
    "//base",
    "//chrome:resources",
    "//chrome:strings",
    "//chrome/browser",
    "//chrome/common",
  ]

  if (is_win) {
    deps = [
      "//chrome/install_static/test:test_support",
    ]
  }
}

test("onevn_unit_tests") {
  sources = [
    "//onevn/app/onevn_command_line_helper_unittest.cc",
    "//onevn/browser/autocomplete/onevn_autocomplete_provider_client_unittest.cc",
    "//onevn/browser/autoplay/autoplay_permission_context_unittest.cc",
    "//onevn/browser/onevn_resources_util_unittest.cc",
    "//onevn/browser/onevn_stats_updater_unittest.cc",
    "//onevn/browser/download/onevn_download_item_model_unittest.cc",
    "//onevn/browser/tor/mock_tor_profile_service_impl.cc",
    "//onevn/browser/tor/mock_tor_profile_service_impl.h",
    "//onevn/browser/tor/mock_tor_profile_service_factory.cc",
    "//onevn/browser/tor/mock_tor_profile_service_factory.h",
    "//onevn/browser/metrics/metrics_reporting_util_unittest_linux.cc",
    "//onevn/browser/net/onevn_ad_block_tp_network_delegate_helper_unittest.cc",
    "//onevn/browser/net/onevn_common_static_redirect_network_delegate_helper_unittest.cc",
    "//onevn/browser/net/onevn_httpse_network_delegate_helper_unittest.cc",
    "//onevn/browser/net/onevn_network_delegate_base_unittest.cc",
    "//onevn/browser/net/onevn_referrals_network_delegate_helper_unittest.cc",
    "//onevn/browser/net/onevn_site_hacks_network_delegate_helper_unittest.cc",
    "//onevn/browser/net/onevn_static_redirect_network_delegate_helper_unittest.cc",
    "//onevn/browser/net/onevn_tor_network_delegate_helper_unittest.cc",
    "//onevn/browser/net/onevn_translate_redirect_network_delegate_helper_unittest.cc",
    "//onevn/browser/profiles/tor_unittest_profile_manager.cc",
    "//onevn/browser/profiles/tor_unittest_profile_manager.h",
    "//onevn/browser/profiles/onevn_profile_manager_unittest.cc",
    "//onevn/browser/resources/settings/reset_report_uploader_unittest.cc",
    "//onevn/browser/resources/settings/brandcode_config_fetcher_unittest.cc",
    "//onevn/browser/themes/onevn_theme_service_unittest.cc",
    "//onevn/chromium_src/chrome/browser/external_protocol/external_protocol_handler_unittest.cc",
    "//onevn/chromium_src/chrome/browser/history/history_utils_unittest.cc",
    "//onevn/chromium_src/chrome/browser/shell_integration_unittest_mac.cc",
    "//onevn/chromium_src/chrome/browser/signin/account_consistency_disabled_unittest.cc",
    "//onevn/chromium_src/chrome/browser/ui/bookmarks/onevn_bookmark_context_menu_controller_unittest.cc",
    "//onevn/chromium_src/components/metrics/enabled_state_provider_unittest.cc",
    "//onevn/chromium_src/components/search_engines/onevn_template_url_prepopulate_data_unittest.cc",
    "//onevn/chromium_src/components/search_engines/onevn_template_url_service_util_unittest.cc",
    "//onevn/chromium_src/components/translate/core/browser/translate_manager_unittest.cc",
    "//onevn/chromium_src/components/version_info/onevn_version_info_unittest.cc",
    "//onevn/chromium_src/net/cookies/onevn_canonical_cookie_unittest.cc",
    "//onevn/common/onevn_content_client_unittest.cc",
    "//onevn/common/importer/onevn_mock_importer_bridge.cc",
    "//onevn/common/importer/onevn_mock_importer_bridge.h",
    "//onevn/common/shield_exceptions_unittest.cc",
    "//onevn/common/tor/tor_test_constants.cc",
    "//onevn/common/tor/tor_test_constants.h",
    "//onevn/components/assist_ranker/ranker_model_loader_impl_unittest.cc",
    "//onevn/components/onevn_shields/browser/ad_block_regional_service_unittest.cc",
    "//onevn/components/onevn_shields/browser/https_everywhere_recently_used_cache_unittest.cpp",
    "//onevn/components/onevn_sync/bookmark_order_util_unittest.cc",
    "//onevn/components/onevn_sync/onevn_sync_service_unittest.cc",
    "//onevn/components/onevn_sync/client/bookmark_change_processor_unittest.cc",
    "//onevn/components/onevn_webtorrent/browser/net/onevn_torrent_redirect_network_delegate_helper_unittest.cc",
    "//onevn/components/invalidation/fcm_unittest.cc",
    "//onevn/components/gcm_driver/gcm_unittest.cc",
    "//onevn/components/invalidation/push_client_channel_unittest.cc",
    "//onevn/components/omnibox/browser/topsites_provider_unittest.cc",
    "//onevn/components/rappor/log_uploader_unittest.cc",
    "//onevn/third_party/libaddressinput/chromium/chrome_metadata_source_unittest.cc",
    "//chrome/common/importer/mock_importer_bridge.cc",
    "//chrome/common/importer/mock_importer_bridge.h",
    "../browser/importer/chrome_profile_lock_unittest.cc",
    "../utility/importer/chrome_importer_unittest.cc",
    "../utility/importer/onevn_importer_unittest.cc",
    "../utility/importer/firefox_importer_unittest.cc",
    "../../components/domain_reliability/test_util.cc",
    "../../components/domain_reliability/test_util.h",
  ]

  if (onevn_ads_enabled) {
    sources += [
      "//onevn/components/onevn_ads/browser/ads_service_impl_unittest.cc"
    ]
  }

  if (onevn_rewards_enabled) {
    sources += [
      "//onevn/vendor/bat-native-ledger/src/bat/ledger/internal/media/helper_unittest.cc",
      "//onevn/vendor/bat-native-ledger/src/bat/ledger/internal/media/twitch_unittest.cc",
      "//onevn/vendor/bat-native-ledger/src/bat/ledger/internal/media/youtube_unittest.cc",
      "//onevn/vendor/bat-native-ledger/src/bat/ledger/internal/bat_helper_unittest.cc",
      "//onevn/vendor/bat-native-ledger/src/bat/ledger/internal/bat_helper_unittest.h",
      "//onevn/vendor/bat-native-ledger/src/bat/ledger/internal/bat_publishers_unittest.cc",
      "//onevn/vendor/bat-native-ledger/src/bat/ledger/internal/bat_publishers_unittest.h",
      "//onevn/vendor/bat-native-ledger/src/bat/ledger/internal/test/niceware_partial_unittest.cc",
      "//onevn/components/onevn_rewards/browser/publisher_info_database_unittest.cc",
      "//onevn/components/onevn_rewards/browser/rewards_service_impl_unittest.cc",
      "//onevn/vendor/bat-native-ads/src/bat/ads/internal/ads_is_mobile_unittest.cc",
      "//onevn/vendor/bat-native-ads/src/bat/ads/internal/ads_tabs_unittest.cc",
      "//onevn/vendor/bat-native-ads/src/bat/ads/internal/ads_client_mock.cc",
      "//onevn/vendor/bat-native-ads/src/bat/ads/internal/ads_client_mock.h",
      "//onevn/vendor/bat-native-confirmations/src/bat/confirmations/internal/confirmations_create_confirmation_request_unittest.cc",
      "//onevn/vendor/bat-native-confirmations/src/bat/confirmations/internal/confirmations_fetch_payment_token_request_unittest.cc",
      "//onevn/vendor/bat-native-confirmations/src/bat/confirmations/internal/confirmations_get_signed_tokens_request_unittest.cc",
      "//onevn/vendor/bat-native-confirmations/src/bat/confirmations/internal/confirmations_redeem_payment_tokens_request_unittest.cc",
      "//onevn/vendor/bat-native-confirmations/src/bat/confirmations/internal/confirmations_request_signed_tokens_request_unittest.cc",
      "//onevn/vendor/bat-native-confirmations/src/bat/confirmations/internal/confirmations_security_helper_unittest.cc",
      "//onevn/vendor/bat-native-confirmations/src/bat/confirmations/internal/confirmations_string_helper_unittest.cc",
      "//onevn/vendor/bat-native-confirmations/src/bat/confirmations/internal/confirmations_unblinded_tokens_unittest.cc",
      "//onevn/vendor/bat-native-confirmations/src/bat/confirmations/internal/confirmations_client_mock.cc",
      "//onevn/vendor/bat-native-confirmations/src/bat/confirmations/internal/confirmations_client_mock.h",
      "//onevn/vendor/bat-native-usermodel/test/usermodel_unittest.cc",
    ]
  }

  # On Windows, onevn_install_static_unittests covers channel test.
  if (is_mac || is_linux) {
    sources += [
      "//onevn/common/onevn_channel_info_unittest.cc",
    ]
  }

  if (is_win || is_mac) {
    sources += [
      "//onevn/chromium_src/chrome/browser/component_updater/widevine_cdm_component_installer_unittest.cc",
    ]
  }

  if (is_mac) {
    sources += [
      "//onevn/chromium_src/chrome/common/chrome_constants_unittest_mac.cc",
    ]
  }

  data = [
    "data/",
  ]

  configs += [
    "//onevn/build/geolocation",
  ]

  if (is_linux) {
    configs += [
      "//onevn/build/linux:linux_channel_names",
    ]
  }

  deps = [
    "//onevn/components/onevn_rewards/browser:testutil",
    "//onevn/components/onevn_ads/browser:testutil",
    "//onevn/components/onevn_sync:testutil",
    "//onevn/vendor/bat-native-rapidjson",
    "//chrome:browser_dependencies",
    "//chrome:child_dependencies",
    "//chrome/test:test_support",
    "//components/prefs",
    "//components/prefs:test_support",
    "//components/version_info",
    "//content/test:test_support",
    "//components/signin/core/browser",
    "//components/sync_preferences",
    "//components/translate/core/browser:test_support",
    "//content/public/common",
    "//third_party/cacheinvalidation",
  ]

  if (onevn_rewards_enabled) {
    deps += [
      "//onevn/vendor/bat-native-usermodel",
      "//onevn/vendor/bat-native-ads",
      "//onevn/vendor/bat-native-confirmations",
      "//onevn/vendor/challenge_bypass_ristretto_ffi",
      "//onevn/vendor/bat-native-ledger",
    ]

    configs += [ "//onevn/vendor/bat-native-ledger:internal_config" ]
    configs += [ "//onevn/vendor/bat-native-ads:internal_config" ]
    configs += [ "//onevn/vendor/bat-native-confirmations:internal_config" ]
  }

  if (bundle_widevine_cdm) {
    sources += [
      "//onevn/browser/widevine/onevn_widevine_bundle_manager_unittest.cc",
    ]
    deps += [ "//third_party/widevine/cdm:headers" ]
  }

  public_deps = [
    "//base",
    "//base/test:test_support",
    "//onevn:browser_dependencies",
    "//onevn/browser",
    "//onevn/common",
    "//onevn/components/content_settings/core/browser",
    "//onevn/renderer",
    "//onevn/utility",
    ":onevn_test_support_unit",
    "//testing/gtest",
  ]

  deps += [
    "//onevn/browser/safebrowsing",
  ]

  include_dirs = []

  if (is_win) {
    sources += [
      "//onevn/chromium_src/chrome/install_static/onevn_install_details_unittest.cc",
      "//onevn/chromium_src/chrome/install_static/onevn_install_modes_unittest.cc",
      "//onevn/chromium_src/chrome/install_static/onevn_install_util_unittest.cc",
      "//onevn/chromium_src/chrome/install_static/onevn_product_install_details_unittest.cc",
      "//onevn/chromium_src/chrome/install_static/onevn_user_data_dir_win_unittest.cc",
      "//onevn/chromium_src/chrome/installer/mini_installer/onevn_mini_installer_unittest.cc",
    ]
    include_dirs += [ "$target_gen_dir" ]
    deps += [
      "//base",
      "//base/test:run_all_unittests",
      "//base/test:test_support",
      "//chrome/install_static:install_static_util",
      "//chrome/install_static/test:test_support",
      "//chrome/installer/mini_installer:lib",
      "//testing/gmock",
      "//testing/gtest",
    ]
  }
}

group("onevn_browser_tests_deps") {
  if (onevn_chromium_build) {
    # force these to build for tests
    deps = [
      "//onevn:browser_dependencies",
      "//onevn:child_dependencies",
      "//onevn:onevn",
    ]
  }
}

static_library("browser_test_support") {
  testonly = true
  deps = [
    "//chrome/test:test_support",
  ]

  sources = [
    "base/onevn_test_launcher_delegate.cc",
    "base/onevn_test_launcher_delegate.h",
  ]
}

static_library("browser_tests_runner") {
  testonly = true

  deps = [
    ":browser_test_support",
  ]

  sources = [
    "base/browser_tests_main.cc",
  ]
}

test("onevn_browser_tests") {
  sources = [
    "//onevn/app/onevn_main_delegate_browsertest.cc",
    "//onevn/browser/autocomplete/onevn_autocomplete_provider_client_browsertest.cc",
    "//onevn/browser/onevn_scheme_load_browsertest.cc",
    "//onevn/chromium_src/chrome/browser/google/chrome_google_url_tracker_client_browsertest.cc",
    "//onevn/chromium_src/components/content_settings/core/browser/onevn_content_settings_registry_browsertest.cc",
    "//onevn/chromium_src/third_party/blink/renderer/modules/battery/navigator_batterytest.cc",
    "//onevn/chromium_src/third_party/blink/renderer/modules/bluetooth/navigator_bluetoothtest.cc",
    "//onevn/chromium_src/third_party/blink/renderer/modules/plugins/navigator_pluginstest.cc",
    "//onevn/common/extensions/onevn_pdf_download_browsertest.cc",
    "//onevn/browser/autoplay/autoplay_permission_context_browsertest.cc",
    "//onevn/browser/onevn_content_browser_client_browsertest.cc",
    "//onevn/browser/onevn_features_browsertest.cc",
    "//onevn/browser/onevn_profile_prefs_browsertest.cc",
    "//onevn/browser/onevn_resources_browsertest.cc",
    "//onevn/browser/onevn_stats_updater_browsertest.cc",
    "//onevn/browser/browsing_data/onevn_clear_browsing_data_browsertest.cc",
    "//onevn/browser/devtools/onevn_devtools_ui_bindings_browsertest.cc",
    "//onevn/browser/extensions/onevn_tor_client_updater_browsertest.cc",
    "//onevn/browser/extensions/onevn_extension_functional_test.cc",
    "//onevn/browser/extensions/onevn_extension_functional_test.h",
    "//onevn/browser/extensions/api/onevn_shields_api_browsertest.cc",
    "//onevn/browser/extensions/api/onevn_theme_api_browsertest.cc",
    "//onevn/browser/extensions/onevn_theme_event_router_browsertest.cc",
    "//onevn/browser/net/onevn_network_delegate_browsertest.cc",
    "//onevn/browser/net/onevn_network_delegate_hsts_fingerprinting_browsertest.cc",
    "//onevn/browser/renderer_context_menu/onevn_mock_render_view_context_menu.cc",
    "//onevn/browser/renderer_context_menu/onevn_mock_render_view_context_menu.h",
    "//onevn/browser/renderer_context_menu/onevn_spelling_menu_observer_browsertest.cc",
    "//onevn/browser/search_engines/search_engine_provider_service_browsertest.cc",
    "//onevn/browser/ui/onevn_dark_mode_observer_browsertest_mac.mm",
    "//onevn/browser/ui/content_settings/onevn_autoplay_blocked_image_model_browsertest.cc",
    "//onevn/browser/ui/views/onevn_actions/onevn_actions_container_browsertest.cc",
    "//onevn/browser/ui/views/profiles/onevn_profile_chooser_view_browsertest.cc",
    "//onevn/browser/ui/webui/onevn_new_tab_ui_browsertest.cc",
    "//onevn/browser/ui/webui/onevn_welcome_ui_browsertest.cc",
    "//onevn/browser/ui/toolbar/onevn_app_menu_model_browsertest.cc",
    "//onevn/chromium_src/third_party/blink/public/platform/disable_client_hints_browsertest.cc",
    "//onevn/common/onevn_channel_info_browsertest.cc",
    "//onevn/components/onevn_shields/browser/ad_block_service_browsertest.cc",
    "//onevn/components/onevn_shields/browser/https_everywhere_service_browsertest.cc",
    "//onevn/components/onevn_shields/browser/referrer_whitelist_service_browsertest.cc",
    "//onevn/components/onevn_shields/browser/tracking_protection_service_browsertest.cc",
    "//onevn/browser/extensions/onevn_component_loader_browsertest.cc",
    "//onevn/browser/extensions/onevn_extension_provider_browsertest.cc",
    "//onevn/renderer/onevn_content_settings_observer_browsertest.cc",
    "//onevn/renderer/onevn_content_settings_observer_autoplay_browsertest.cc",
    "//onevn/renderer/onevn_content_settings_observer_flash_browsertest.cc",
    "//onevn/browser/themes/onevn_theme_service_browsertest.cc",
    "//chrome/browser/extensions/browsertest_util.cc",
    "//chrome/browser/extensions/browsertest_util.h",
    "//chrome/browser/extensions/extension_browsertest.cc",
    "//chrome/browser/extensions/extension_browsertest.h",
    "//chrome/browser/extensions/extension_function_test_utils.cc",
    "//chrome/browser/extensions/extension_function_test_utils.h",
    "//chrome/browser/extensions/updater/extension_cache_fake.cc",
    "//chrome/browser/extensions/updater/extension_cache_fake.h",
  ]

  deps = []
  if (bundle_widevine_cdm || enable_widevine_cdm_component) {
    sources += [
      "//onevn/browser/widevine/widevine_permission_request_browsertest.cc",
    ]
  }

  if (onevn_rewards_enabled) {
    sources += [
      "//onevn/components/onevn_rewards/browser/rewards_notification_service_browsertest.cc",
      "//onevn/components/onevn_rewards/browser/rewards_service_browsertest.cc",
    ]

    deps += [
      "//onevn/vendor/bat-native-ads",
      "//onevn/vendor/bat-native-confirmations",
      "//onevn/vendor/bat-native-ledger",
    ]

    configs += [ "//onevn/vendor/bat-native-ledger:internal_config" ]
    configs += [ "//onevn/vendor/bat-native-ads:internal_config" ]
    configs += [ "//onevn/vendor/bat-native-confirmations:internal_config" ]
  }

  if (is_win || is_linux) {
    sources += [
      "//onevn/browser/renderer_context_menu/onevn_spelling_options_submenu_observer_browsertest.cc",
    ]
  }

  # API tests
  sources += [
    "//onevn/browser/extensions/onevn_shields_apitest.cc",
    "//chrome/browser/extensions/extension_apitest.cc",
    "//chrome/browser/extensions/extension_apitest.h",
  ]

  if (is_linux && is_official_build) {
    sources += [
      # OnevnViewsDelegateLinuxBrowserTest only works in release(official) build.
      "//onevn/browser/ui/views/onevn_views_delegate_linux_browsertest.cc",
    ]
    configs += [
      "//onevn/build/linux:linux_channel_names",
    ]
  }

  defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]
  deps += [
    "//chrome/browser/ui",
    "//ppapi/buildflags",
    ":onevn_browser_tests_deps",
    ":browser_tests_runner",
    "//testing/gmock",
  ]
  data_deps = [
    "//ppapi:ppapi_tests",
    "//ppapi:power_saver_test_plugin",
  ]
}

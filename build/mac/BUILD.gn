import("//onevn/build/config.gni")
import("//build/config/mac/base_rules.gni")

declare_args() {
  # find with `security find-identity -v -p codesigning`
  mac_signing_identifier = ""
  mac_installer_signing_identifier = ""
  mac_signing_keychain = "login"
  mac_signing_output_prefix = "signing"
}

_packaging_dir = "$root_out_dir/$chrome_product_full_name Packaging"
keychain_db = getenv("HOME") + "/Library/Keychains/${mac_signing_keychain}.keychain-db"

_target_app_path = "$root_out_dir/$mac_signing_output_prefix/$onevn_exe"

group("onevn") {}

action("sign_app") {
  onevn_app = "$root_build_dir/$onevn_exe"

  script = "//build/gn_run_binary.py"
  shell_script = "//onevn/build/mac/sign_app.sh"

  deps = [
    "//onevn:chrome_app",
    "//chrome/installer/mac"
  ]

  if (is_official_build) {
    if (onevn_channel == "") {
      provisioning_profile = "//onevn/build/mac/release.provisionprofile"
    } else {
      provisioning_profile = "//onevn/build/mac/${onevn_channel}.provisionprofile"
    }
  } else {
    # an empty dummy file just to simplify things
    provisioning_profile = "//onevn/build/mac/dummy.provisionprofile"
  }
  inputs = [
    shell_script,
    onevn_app,
    provisioning_profile,
    "$_packaging_dir/sign_app.sh",
    "$_packaging_dir/sign_versioned_dir.sh",
    "$_packaging_dir/entitlements.plist",
  ]
  outputs = [ _target_app_path ]
  args = [
    rebase_path(shell_script, root_out_dir),

    # Use absolute paths needed by codesign
    rebase_path(onevn_app),
    rebase_path(_target_app_path),
    rebase_path(_packaging_dir),
    keychain_db,
    mac_signing_identifier,
    rebase_path(provisioning_profile, root_out_dir),
    rebase_path("$_packaging_dir/entitlements.plist"),
  ]

  if (!is_official_build) {
    args += [ "--development" ]
  }
}

action("create_pkg") {
  output = "$root_out_dir/unsigned/$onevn_pkg"
  script = "//build/gn_run_binary.py"
  shell_script = "//onevn/build/mac/create_pkg.sh"
  inputs = [
    script,
    shell_script,
  ]
  outputs = [ output ]
  args = [
    rebase_path(shell_script, root_build_dir),
    rebase_path(_target_app_path, root_build_dir),
    rebase_path("//onevn/build/mac/pkg-scripts"),
    rebase_path("$root_out_dir/Onevn Browser Component.plist"),
    "unsigned/$onevn_pkg",
  ]

  deps = [":sign_app"]
}

action("sign_pkg") {
  script = "//build/gn_run_binary.py"
  shell_script = "sign_pkg.sh"
  inputs = [
    script,
    shell_script,
    "$root_out_dir/unsigned/$onevn_pkg",
  ]
  outputs = [ "${root_out_dir}/$mac_signing_output_prefix/$onevn_pkg" ]
  args = [
    rebase_path(shell_script, root_build_dir),
    rebase_path("$root_out_dir/unsigned/$onevn_pkg"),
    rebase_path("$root_out_dir/$onevn_pkg"),
    keychain_db,
    mac_installer_signing_identifier,
  ]

  deps = [":create_pkg"]
}

action("create_dmg") {
  output = "$root_out_dir/unsigned/$onevn_dmg"
  script = "//build/gn_run_binary.py"
  shell_script = "//chrome/installer/mac/pkg-dmg"
  inputs = [
    script,
    shell_script,
  ]
  outputs = [ output ]
  args = [
    rebase_path(shell_script, root_build_dir),
    "--source", "/var/empty",
    "--target", "unsigned/$onevn_dmg",
    "--format", "UDBZ",
    "--verbosity", "0",
    "--volname", "$chrome_product_full_name",
    "--copy", rebase_path(_target_app_path, root_build_dir),
    "--tempdir", "/tmp",
    "--symlink", "/Applications",
  ]

  deps = [":sign_app"]
}

action("create_unsigned_dmg") {
  onevn_app = "$root_build_dir/$onevn_exe"
  output = "$root_out_dir/unsigned_dmg/$onevn_dmg"
  script = "//build/gn_run_binary.py"
  shell_script = "//chrome/installer/mac/pkg-dmg"
  inputs = [
    script,
    shell_script,
  ]
  outputs = [ output ]
  args = [
    rebase_path(shell_script, root_build_dir),
    "--source", "/var/empty",
    "--target", "unsigned_dmg/$onevn_dmg",
    "--format", "UDBZ",
    "--verbosity", "0",
    "--volname", "$chrome_product_full_name",
    "--copy", rebase_path(onevn_app, root_build_dir),
    "--tempdir", "/tmp",
    "--symlink", "/Applications",
  ]

  deps = [
    "//onevn:chrome_app",
    "//chrome/installer/mac"
  ]
}

action("sign_dmg") {
  script = "//build/gn_run_binary.py"
  shell_script = "sign_dmg.sh"
  inputs = [
    script,
    shell_script,
    "$root_out_dir/unsigned/$onevn_dmg",
  ]
  outputs = [ "${root_out_dir}/$mac_signing_output_prefix/$onevn_dmg" ]
  args = [
    rebase_path(shell_script, root_build_dir),
    rebase_path("$root_out_dir/unsigned/$onevn_dmg"),
    rebase_path("$root_out_dir/$onevn_dmg"),
    keychain_db,
    mac_signing_identifier,
    "-r=designated => identifier \"$chrome_product_full_name\" and certificate leaf = H\"$mac_signing_identifier\"",
  ]

  deps = [":create_dmg"]
}

group("create_dist_mac") {
  deps = [ ":create_dmg" ]

  deps += [
    ":sign_app",
    ":sign_dmg",
  ]
}

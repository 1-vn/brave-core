import("//onevn/build/features.gni")
import("//build/util/branding.gni")

declare_args() {
  # Release channel support.
  # "beta" for beta release.
  # "dev" for dev channel release.
  # "nightly" for nightly channel release.
  # "" for stable channel release.
  onevn_channel = ""

  base_sparkle_update_url = ""

  onevn_dsa_file = "dsa_pub.pem"

  onevn_product_name = ""
  onevn_project_name = ""
  onevn_exe = ""
  onevn_version_major = ""
  onevn_version_minor = ""
  onevn_version_build = ""
  onevn_version_patch = 0
  chrome_version_string = ""
  chrome_version_major = ""
  build_omaha = false
  tag_ap =""

  skip_signing = false
}

if (base_sparkle_update_url == "") {
  base_sparkle_update_url = "https://updates.onevnsoftware.com/sparkle/Onevn-Browser"
}
_update_channel = onevn_channel
if (_update_channel == "") {
  _update_channel = "stable"
}
onevn_feed_url = "$base_sparkle_update_url/$_update_channel/appcast.xml"

onevn_version = "$onevn_version_major.$onevn_version_minor.$onevn_version_build"

if (onevn_exe == "") {
  onevn_exe = onevn_project_name
}

onevn_dist_dir = "$root_out_dir/dist"
if (is_win) {
  onevn_exe = "$onevn_exe.exe"
  onevn_underline_full_version = "_$chrome_version_major" + "_$onevn_version_major" + "_$onevn_version_minor" + "_$onevn_version_build"
  _channel = ""
  onevn_app_guid = "{AFE6A462-C574-4B8A-AF43-4CC60DF4563B}"
  if (is_official_build) {
    if (onevn_channel == "beta") {
      _channel = "Beta"
      onevn_app_guid = "{103BD053-949B-43A8-9120-2E424887DE11}"
    } else if (onevn_channel == "dev") {
      _channel = "Dev"
      onevn_app_guid = "{CB2150F2-595F-4633-891A-E39720CE0531}"
    } else if (onevn_channel == "nightly") {
      _channel = "Nightly"
      onevn_app_guid = "{C6CB981E-DB30-4876-8639-109F8933582C}"
    } else {
      assert(onevn_channel == "", "Unknown channel name")
    }
  } else {
    _channel = "Development"
  }
  _arch = ""
  if (target_cpu == "x86") {
    _arch = "32"
  }
  onevn_installer_exe = "onevn_installer$onevn_underline_full_version.exe"
  onevn_stub_installer_exe = "OnevnBrowser$_channel" + "Setup$_arch$onevn_underline_full_version.exe"
  onevn_silent_stub_installer_exe = "OnevnBrowserSilent$_channel" + "Setup$_arch$onevn_underline_full_version.exe"
  onevn_untagged_stub_installer_exe = "OnevnBrowserUntagged$_channel" + "Setup$_arch$onevn_underline_full_version.exe"
  onevn_standalone_installer_exe = "OnevnBrowserStandalone$_channel" + "Setup$_arch$onevn_underline_full_version.exe"
  onevn_silent_installer_exe = "OnevnBrowserStandaloneSilent$_channel" + "Setup$_arch$onevn_underline_full_version.exe"
  onevn_untagged_installer_exe = "OnevnBrowserStandaloneUntagged$_channel" + "Setup$_arch$onevn_underline_full_version.exe"
} else if (is_mac) {
  onevn_exe = "$chrome_product_full_name.app"
  onevn_dmg = "$chrome_product_full_name.dmg"
  onevn_pkg = "$chrome_product_full_name.pkg"

  onevn_product_dir_name_suffix = ""
  if (is_official_build) {
    if (onevn_channel == "beta") {
      onevn_product_dir_name_suffix = "-Beta"
    } else if (onevn_channel == "dev") {
      onevn_product_dir_name_suffix = "-Dev"
    } else if (onevn_channel == "nightly") {
      onevn_product_dir_name_suffix = "-Nightly"
    } else {
      assert(onevn_channel == "", "Unknown channel name")
    }
  } else {
    onevn_product_dir_name_suffix = "-Development"
  }
  onevn_product_dir_name = "OnevnSoftware/Onevn-Browser$onevn_product_dir_name_suffix"
}

onevn_platform = "darwin"
if (is_win) {
  onevn_platform = "win32"
} else if (is_linux) {
  onevn_platform = "linux"
}

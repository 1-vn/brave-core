# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# You can obtain one at http://mozilla.org/MPL/2.0/.

import("//build/config/locales.gni")
import("//chrome/common/features.gni")
import("//extensions/buildflags/buildflags.gni")
import("//tools/grit/repack.gni")
import("//ui/base/ui_features.gni")

# Generates a rule to repack a set of resources, substituting a given string
# in for the percentage (e.g. "100", "200"). It generates the repacked files in
# the "gen" directory, and then introduces a copy rule to copy it to the root
# build directory.
#
# Argument:
#   percent [required]
#      String to substitute for the percentage.
template("onevn_repack_percent") {
  percent = invoker.percent

  repack(target_name) {
    forward_variables_from(invoker,
                           [
                             "copy_data_to_bundle",
                             "repack_whitelist",
                             "visibility",
                           ])

    # All sources should also have deps for completeness.
    sources = [
      "$root_gen_dir/onevn/onevn_theme_resources_${percent}_percent.pak",
    ]

    deps = [
      "//onevn/app/theme:onevn_theme_resources",
    ]

    if (defined(invoker.deps)) {
      deps += invoker.deps
    }

    output = "${invoker.output_dir}/onevn_${percent}_percent.pak"
  }
}

template("onevn_extra_paks") {
  repack(target_name) {
    forward_variables_from(invoker,
                           [
                             "copy_data_to_bundle",
                             "repack_whitelist",
                             "visibility",
                           ])
    output = "${invoker.output_dir}/onevn_resources.pak"
    sources = [
      "$root_gen_dir/onevn/onevn_generated_resources.pak",
      "$root_gen_dir/onevn/onevn_unscaled_resources.pak",
      "$root_gen_dir/onevn/browser/resources/md_extensions/onevn_extensions_resources.pak",
      "$root_gen_dir/onevn/browser/resources/settings/onevn_settings_resources.pak",
      "$root_gen_dir/components/onevn_components_resources.pak",
      "$root_gen_dir/onevn/components/onevn_ads/resources/onevn_ads_resources.pak",
      "$root_gen_dir/onevn/components/onevn_rewards/resources/onevn_rewards_resources.pak",
      "$root_gen_dir/onevn/components/onevn_rewards/resources/extension/onevn_rewards_extension_resources.pak",
      "$root_gen_dir/onevn/components/onevn_webtorrent/onevn_webtorrent_resources.pak",
      "$root_gen_dir/onevn/components/onevn_sync/onevn_sync_resources.pak",
      "$root_gen_dir/onevn/ui/webui/resources/onevn_webui_resources.pak",
      "$root_gen_dir/onevn/components/onevn_extension/onevn_extension_resources.pak",
    ]

    deps = [
      "//onevn/app:onevn_generated_resources_grit",
      "//onevn/app/theme:onevn_unscaled_resources",
      "//onevn/components/onevn_ads/resources",
      "//onevn/components/onevn_rewards/resources",
      "//onevn/components/onevn_rewards/resources/extension:resources",
      "//onevn/components/onevn_webtorrent:resources",
      "//onevn/components/resources",
      "//onevn/components/resources:strings",
      "//onevn/browser/resources/md_extensions:resources",
      "//onevn/browser/resources/settings:resources",
      "//onevn/common/extensions/api",
      "//onevn/components/onevn_sync:resources",
      "//onevn/components/onevn_extension:resources",
      "//onevn/ui/webui/resources",
    ]

    if (defined(invoker.deps)) {
      deps += invoker.deps
    }
    if (defined(invoker.additional_paks)) {
      sources += invoker.additional_paks
    }
  }
}

# Defines repack() targets used by Onevn. Specifically:
# * onevn_100_percent.pak
# * onevn_200_percent.pak (optionally)
# * resources.pak
#
# Paramters:
#   output_dir [required]: Directory to output .pak files.
#   additional_extra_paks: List of extra .pak sources for resources.pak.
#   locale_whitelist: if set, override repack_whitelist for locale .pak files.
#   copy_data_to_bundle:
#   deps:
#   output_dir:
#   public_deps:
#   repack_whitelist:
#   visibility:
#     Normal meanings.
#
template("onevn_paks") {
  onevn_repack_percent("${target_name}_100_percent") {
    percent = "100"
    forward_variables_from(invoker,
                           [
                             "copy_data_to_bundle",
                             "deps",
                             "output_dir",
                             "repack_whitelist",
                             "visibility",
                           ])
  }

  if (enable_hidpi) {
    onevn_repack_percent("${target_name}_200_percent") {
      percent = "200"
      forward_variables_from(invoker,
                             [
                               "copy_data_to_bundle",
                               "deps",
                               "output_dir",
                               "repack_whitelist",
                               "visibility",
                             ])
    }
  }

  onevn_extra_paks("${target_name}_extra") {
    forward_variables_from(invoker,
                           [
                             "copy_data_to_bundle",
                             "deps",
                             "output_dir",
                             "repack_whitelist",
                             "visibility",
                           ])
    if (defined(invoker.additional_extra_paks)) {
      additional_paks = invoker.additional_extra_paks
    }
  }

  group(target_name) {
    forward_variables_from(invoker, [ "deps" ])
    public_deps = [
      ":${target_name}_100_percent",
      ":${target_name}_extra",
    ]
    if (enable_hidpi) {
      public_deps += [ ":${target_name}_200_percent" ]
    }
    if (defined(invoker.public_deps)) {
      public_deps += invoker.public_deps
    }
  }
}

import("//tools/json_schema_compiler/json_features.gni")
import("//tools/json_schema_compiler/json_schema_api.gni")
import("//tools/grit/grit_rule.gni")
json_features("api_features") {
  feature_type = "APIFeature"
  method_name = "AddOneVNAPIFeatures"
  sources = [
    "//chrome/common/extensions/api/_api_features.json",
    "//extensions/common/api/_api_features.json",
    "_api_features.json",
  ]
}

json_features("permission_features") {
  feature_type = "PermissionFeature"
  method_name = "AddOneVNPermissionFeatures"
  sources = [
    "//chrome/common/extensions/api/_permission_features.json",
    "//extensions/common/api/_permission_features.json",
  ]
}

json_features("manifest_features") {
  feature_type = "ManifestFeature"
  method_name = "AddOneVNManifestFeatures"
  sources = [
    "//chrome/common/extensions/api/_manifest_features.json",
    "//extensions/common/api/_manifest_features.json",
    "_manifest_features.json",
  ]
}

json_features("behavior_features") {
  feature_type = "BehaviorFeature"
  method_name = "AddOneVNBehaviorFeatures"
  sources = [
    "//extensions/common/api/_behavior_features.json",
  ]
}

onevn_extensions_api_schema_sources = [
  "onevn_shields.json",
  "onevn_rewards.json",
  "onevn_theme.json",
  "rewards_notifications.json",
  "onevn_sync.json",
]
onevn_extensions_api_uncompiled_sources = []
onevn_extensions_api_root_namespace = "extensions::api::%(namespace)s"
onevn_extensions_api_schema_include_rules =
    "//onevn/common/extensions/api:extensions::api::%(namespace)s"

onevn_extensions_api_deps = [
  "//chrome/common/extensions/api",
  "//extensions/common/api",
  "//skia",
  ":api_features",
  ":permission_features",
  ":manifest_features",
  ":behavior_features",
  ":resources",
]

grit("resources") {
  source = "onevn_api_resources.grd"
  outputs = [
    "grit/onevn_api_resources.h",
    "onevn_api_resources.pak",
  ]
  resource_ids = "//onevn/browser/resources/resource_ids"
}

function_registration("generated_api_registration") {
  sources = onevn_extensions_api_schema_sources
  impl_dir = "//onevn/common/extensions/api/"
  bundle_name = "OneVN"
  root_namespace = onevn_extensions_api_root_namespace

  deps = [
    "//extensions/common",
  ]

  visibility = [ ":api" ]
}


generated_json_strings("generated_api_json_strings") {
  sources = onevn_extensions_api_schema_sources +
            onevn_extensions_api_uncompiled_sources

  configs = [ "//build/config:precompiled_headers" ]
  bundle_name = "OneVN"
  schema_include_rules = onevn_extensions_api_schema_include_rules

  root_namespace = onevn_extensions_api_root_namespace
  deps = onevn_extensions_api_deps

  visibility = [ ":api" ]
}

generated_types("generated_api_types") {
  sources = onevn_extensions_api_schema_sources
  configs = [ "//build/config:precompiled_headers" ]
  schema_include_rules = onevn_extensions_api_schema_include_rules

  root_namespace = onevn_extensions_api_root_namespace
  deps = onevn_extensions_api_deps
  visibility = [ ":api" ]
}

# Public Targets

group("api") {
  public_deps = [
    ":generated_api_json_strings",
    ":generated_api_registration",
    ":generated_api_types",
  ]
}

import("//onevn/build/config.gni")

group("chromedriver_dist_resources") {
  deps = [
    ":strip_and_dump_chromedriver_symbols"
  ]
}

action("strip_and_dump_chromedriver_symbols") {
  script = "//build/linux/dump_app_syms.py"
  dump_syms_label = "//third_party/breakpad:dump_syms($host_toolchain)"
  dump_syms_binary =
          get_label_info(dump_syms_label, "root_out_dir") + "/" + "dump_syms"

  chromedriver_binary = "$onevn_dist_dir/chromedriver"
  if (current_cpu == "x86") {
  # Use "ia32" instead of "x86" for GYP compat.
    chromedriver_symbol_file = "$root_out_dir/chromedriver.breakpad.ia32"
  } else {
    chromedriver_symbol_file = "$root_out_dir/chromedriver.breakpad.$current_cpu"
  }

  inputs = [ chromedriver_binary, dump_syms_binary ]
  outputs = [ chromedriver_symbol_file ]

  args = [
    "./" + rebase_path(dump_syms_binary, root_build_dir),
    "1",  # strip_binary = true
    rebase_path(chromedriver_binary, root_build_dir),
    rebase_path(chromedriver_symbol_file, root_build_dir),
  ]

  deps = [
    dump_syms_label,
    "//onevn:onevn_chromedriver_dist_resources",
  ]
}


action("generate_breakpad_symbols") {
  symbols_dir = "$onevn_dist_dir/$onevn_project_name.breakpad.syms"
  outputs = [
    symbols_dir
  ]

  start = [ onevn_project_name ]

  script = "//onevn/tools/posix/generate_breakpad_symbols.py"
  args = [
    "--symbols-dir=" + rebase_path(symbols_dir),
    "--jobs=16",
    "--build-dir=" + rebase_path(root_out_dir),
    "--binary=$start",
    "--libchromiumcontent-dir=" + rebase_path("//"),
    "--clear",
  ]

  deps = [
    "//chrome", # To be sure onevn executable is ready now
    "//third_party/breakpad:dump_syms",
  ]
}

group("symbol_dist_resources") {
  public_deps = [
    ":generate_breakpad_symbols",
    ":strip_and_dump_symbols",
  ]
}

group("dist_resources") {}

action("strip_and_dump_symbols") {
  script = "//build/linux/dump_app_syms.py"

  dump_syms_label = "//third_party/breakpad:dump_syms($host_toolchain)"
  dump_syms_binary =
          get_label_info(dump_syms_label, "root_out_dir") + "/" + "dump_syms"

  deps = [
    "//onevn:onevn_dist_resources",
    # TODO(bridiver) - resolve duplicate symbol generation
    ":generate_breakpad_symbols",
    dump_syms_label,
  ]
  onevn_binary = "$onevn_dist_dir/onevn"
  if (current_cpu == "x86") {
  # Use "ia32" instead of "x86" for GYP compat.
    onevn_symbol_file = "$root_out_dir/onevn.breakpad.ia32"
  } else {
    onevn_symbol_file = "$root_out_dir/onevn.breakpad.$current_cpu"
  }

  inputs = [ onevn_binary, dump_syms_binary ]
  outputs = [ onevn_symbol_file ]

  args = [
    "./" + rebase_path(dump_syms_binary, root_build_dir),
    "1",  # strip_binary = true
    rebase_path(onevn_binary, root_build_dir),
    rebase_path(onevn_symbol_file, root_build_dir),
  ]
}
